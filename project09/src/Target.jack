class Target { 
    field int position;
    field int height;
    field boolean active;
    
    constructor Target new(int p, int h) { 
        let position = p;
        let height = h;
        let active = true;
        do  render();
        return this;
    }

    method void render() { 
        do  Screen.drawLine(position, 245, position, 245 - height);
        do  Screen.drawRectangle(position - 6, 245 - height - 1, position + 6, 245 - height + 1);
        do  Screen.drawRectangle(position - 8, 245 - height - 10, position + 8, 245 - height - 4);
        return;
    }

    // determine if target is hit
    method boolean intersects(int x, int y) { 
        if (~active) { 
            return false;
        }

        return ((x > (position - 16)) & (x < (position + 16)) & (y > (245 - height - 16)) & (y < (245 - height + 16)));
    }

    method void destroy() { 
        do  Screen.setColor(false);
        do  Screen.drawRectangle(position - 8, 245 - height - 10, position + 8, 245 - height);
        let active = false;
        return;
    }

    method boolean isActive() { 
        return active;
    }
}